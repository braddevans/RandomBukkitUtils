import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'java'
apply plugin: 'maven-publish'

// Gradle Configs
apply from: 'env.gradle'

group = 'uk.co.breadhub'
version = '1.1_b7'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
    google()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
}

dependencies {
    compileOnly 'org.bukkit:bukkit:1.12.2-R0.1-SNAPSHOT'
    compile 'com.mashape.unirest:unirest-java:1.4.9'
    compile 'org.reflections:reflections:0.9.12'
    compile 'com.googlecode.json-simple:json-simple:1.1.1'
}

jar {
    from sourceSets.main.output
    archiveName("output.jar")
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }

    repositories {
        maven {
            url 'https://nexus.breadhub.co.uk/repository/maven-' + (project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases') + '/'
            credentials {
                username project.nexus_username
                password project.nexus_password
            }
        }
    }
}


processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}
